
// build script
buildscript {
	repositories {
		mavenLocal()
		mavenCentral()
		jcenter()
	}
	dependencies {
		classpath 'net.swisstech:swissarmyknife:+'
	}
}

// plugins
plugins {
	id 'java'
	id 'jacoco'
	id 'eclipse'
	id 'maven-publish'
	id 'project-report'
	id 'net.swisstech.eclipseenhancer'   version '1.0.0'
	id 'com.github.johnrengelman.shadow' version '1.2.1'
}

// re-use repositories from buildscript
buildscript.repositories.each { repositories.add(it) }

// java version
sourceCompatibility = 1.7
targetCompatibility = 1.7

// basic infos
description = 'ElasticSearch River plugin for ArangoDB'
group       = 'com.github.triagens.elasticsearch'
version     = '0.4.0-SNAPSHOT'

// project dependencies
ext.versions = [
	  elasticsearch      : '1.4.2'
	, arangodb_wal_client: '1.1.4'
	, httpcomponents     : '4.3.6'
	, jackson            : '1.9.13'
	, json               : '20140107'
	, swissarmyknife     : '1.2.0'
	, logback_classic    : '1.1.2'
	, testng             : '6.8.17'
	, mockito            : '1.10.19'
]

dependencies {
	// Core dependencies
	compile "org.elasticsearch:elasticsearch:${versions.elasticsearch}"
	compile "org.apache.httpcomponents:httpclient:${versions.httpcomponents}"
	compile "org.json:json:${versions.json}"
	compile "org.codehaus.jackson:jackson-mapper-asl:${versions.jackson}"

	// see: https://github.com/stackmagic/arangodb-wal-client
	compile "net.swisstech:arangodb-wal-client:${versions.arangodb_wal_client}"

	// see: https://github.com/stackmagic/swissarmyknife
	compile "net.swisstech:swissarmyknife:${versions.swissarmyknife}"

	// Tests
	testCompile "ch.qos.logback:logback-classic:${versions.logback_classic}"
	testCompile "org.testng:testng:${versions.testng}"
	testCompile "org.mockito:mockito-all:${versions.mockito}"
}

test {
	useTestNG()
}

// task to generate wrapper
task wrapper(type: Wrapper) {
	gradleVersion = '2.2.1'
}

// config to build fat jar
shadowJar {
	mergeServiceFiles()
	dependencies {
		exclude(dependency("org.spatial4j:.*:.*"))
		exclude(dependency("org.apache.lucene:.*:.*"))
		exclude(dependency("org.elasticsearch:.*:.*"))
	}
}

// source-code jar
task sourceJar(type: Jar) {
	from sourceSets.main.allSource
}

// configure maven jar publishing
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
			artifact sourceJar {
				classifier 'sources'
			}
		}
	}
}

// extra configuration for integration tests
configurations {
	intTestCompile
}

sourceSets {
	intTest {
		compileClasspath = sourceSets.main.output + configurations.testRuntime
		runtimeClasspath = sourceSets.main.output + configurations.testRuntime + output
	}
}

// normal unit tests
test {
	useTestNG()

	jacoco {
		destinationFile = file("${buildDir}/jacoco.ut.exec")
	}
}

// setup for integration tests, requires a local installation of arangodb
ext.arangoDbPort         = 18529
ext.arangoDataDir        = "/tmp/river_arangodb${System.currentTimeMillis()}"
ext.elasticsearchPort    = 19200
ext.elasticsearchDataDir = "/tmp/river_elasticsearch${System.currentTimeMillis()}"

task('intTest', type: Test, dependsOn: 'intTestClasses') {
	useTestNG()
	testLogging.showStandardStreams = true
	group          = 'verification'
	testSrcDirs    = sourceSets.intTest.java.srcDirs as List
	testClassesDir = sourceSets.intTest.output.classesDir
	classpath      = sourceSets.intTest.runtimeClasspath
	systemProperty 'ARANGODB_PORT'     , project.arangoDbPort
	systemProperty 'ELASTICSEARCH_PORT', project.elasticsearchPort

	jacoco {
		destinationFile = file("${buildDir}/jacoco.it.exec")
	}
}

jacocoTestReport {
	reports.html.destination = "${reportsDir}/jacoco/${name}"
}
task('jacocoIntTestReport', type: JacocoReport) {
	reports.html.destination = "${reportsDir}/jacoco/${name}"
	executionData intTest
	sourceSets sourceSets.main
}

// start & stop arangodb
afterEvaluate {

	task('startArangoDb') << {
		new File(project.arangoDataDir + "/data").mkdirs()
		project.ext.arangodb_process = net.swisstech.swissarmyknife.sys.linux.BackgroundProcess
			.launch([
				'/usr/sbin/arangod',
				'--server.endpoint'   , "tcp://127.0.0.1:${project.arangoDbPort}",
				'--database.directory', "${project.arangoDataDir}/data",
				'--log.file'          , '+'
			], null)
			.waitForOpenPorts([ project.arangoDbPort ], 10000)
	}

	task('stopArangoDb') << {
		project.arangodb_process.shutdown()
		project.delete(project.arangoDataDir)
	}

	task('startElasticSearch') << {
		new File(project.elasticsearchDataDir + "/log").mkdirs()
		new File(project.elasticsearchDataDir + "/data").mkdirs()
		new File(project.elasticsearchDataDir + "/work").mkdirs()
		project.ext.elasticsearch_process = net.swisstech.swissarmyknife.sys.linux.BackgroundProcess
			.launch([
				'/usr/share/elasticsearch/bin/elasticsearch',
				'--server.endpoint'   , "tcp://127.0.0.1:${project.arangoDbPort}",
				'--database.directory', "${project.arangoDataDir}/data",
				'--log.file'          , '+'
			], null)
			.waitForOpenPorts([ project.arangoDbPort ], 10000)
	}

	task('stopElasticSearch') << {
		project.elasticsearch_process.shutdown()
		project.delete(project.elasticsearchDataDir)
	}

	tasks.intTest.dependsOn('startArangoDb')
	tasks.intTest.finalizedBy('stopArangoDb')
}
